# 살랑 v0.1

> [!TIP]
> 히잉 형아 꼬리 살랑~

난해한 프로그래밍 언어 '살랑'을 소개합니다.

[살랑 실행 환경](https://dragonteros.github.io/sallang/)에서 체험해보세요~

## 예제

### 소문자로 바꿔요
표준 입력으로 대문자를 입력하면 표준 출력으로 소문자를 출력
```
히잉 형아야 꼬리 살랑  # read 1 byte & push to stack
히잉 형아야 꼬리복슬복슬 살랑힛  # push 4
히잉 형도 복슬복슬꼬리나랑같이 살랑살랑  # pop & multiply 8 & push
히잉 헷 꼬리 살랑뀨  # pop & pop & add & push
히잉 형 꼬리도 살랑  # pop & write
```

### Hello, world!
```
히잉 형아야 꼬리가복슬하고귀여워 살랑뀨
히잉 흐아 꼬리너무복슬하고귀여워 살랑살랑
히잉 형 꼬리도 살랑
히잉 누나야아 꼬리가너무복슬하고귀여워 살랑뀨
히잉 누나 꼬리만질래 살랑
히잉 누나야도 꼬리만질래 살랑
히잉 누나야랑 꼬리만질래 살랑
히잉 눈나 꼬리 살랑살랑
히잉 누나 꼬리너무좋아 살랑
히잉 언니야아 꼬리너무좋아 살랑
히잉 언니가 꼬리를 살랑해
히잉 언니 꼬리도 살랑
히잉 오빠야아 꼬리너무좋아 살랑
히잉 오빠도 꼬리복슬복슬보들보들 살랑해
히잉 오빠 꼬리복슬해 살랑
히잉 오빠야는 꼬리따끈해 살랑
히잉 오빠야도 꼬리살랑해 살랑
히잉 오빠 꼬리도 살랑
히잉 오빠 꼬리도 살랑
히잉 필멸자야아 꼬리귀엽지 살랑
히잉 필멸자도 꼬리만져봐 살랑앙
히잉 필멸자 꼬리도 살랑
히 필멸자야아 꼬리귀엽다너무사랑스럽다하늘의별도달도따줄게 살랑뀨
히잉 필멸자 꼬리를 살랑
히 언니야가 꼬리가더귀여운데 살랑뀨
히잉 언니 꼬리도 살랑
히잉 언니야랑 꼬리다같이 살랑
히잉 언니는 꼬리가귀여운나랑평생살자응 살랑헷
히잉 언니 꼬리가 살랑
히 언니야랑 같이꼬리 살랑뀨
히잉 언니야도 꼬리를살랑 살랑
히잉 언니도 꼬리귀여워 살랑앙
히잉 언니 긴꼬리 살랑
히잉 필멸자야아 꼬리만질래 살랑
히잉 필멸자의 귀엽고깜찍한꼬리 살랑헷
히잉 필멸자 꼬리가 살랑
히잉 필멸자야아 복슬한꼬리 살랑
히잉 필멸자 큰꼬리 살랑
히잉 형아야 살랑살랑꼬리 살랑
히잉 형 아꼬리 살랑
히잉 언니 야꼬리 살랑
```

## 명세

살랑 언어는 UTF-8 텍스트로 작성합니다.

살랑 언어는 한글 음절(`U+AC00`~`U+D7A3`)과 개행 문자(`U+000A`)를 제외한 모든 문자를 공백 문자(`U+0020`)과 동일하게 취급하므로, 이하에서도 공백 문자로 이들을 대표하겠습니다.

살랑 프로그램은 개행 문자로 구분된 여러 줄의 구문으로 구성됩니다.

### 구문
구문은 공백 문자들로 구분된 세 개 또는 네 개의 단어로 구성되며, 앞뒤로 공백 문자들이 추가로 존재할 수 있습니다. 단어는 개행 문자나 공백 문자를 포함하지 않는 연속한 한글 음절들을 이야기합니다.

구문을 구성하는 단어를 앞에서부터 차례로 '히잉', '호칭', '꼬리', '살랑'이라고 부르겠습니다. 구문이 세 개의 단어로만 구성된 경우, '살랑'은 빈 문자열입니다.

구문의 두번째 단어('호칭')의 첫 음절은 반드시 다음 5가지 경우 가운데 하나에 해당하여야 합니다.

* 초성이 'ㄴ'이다.
* 초성은 'ㅇ'이고 중성은 'ㅓ'다.
* 초성은 'ㅇ'이고 중성은 'ㅗ'다.
* 초성이 'ㅍ'이다.
* 초성이 'ㅎ'이다.

### 원형
구문의 각 단어는 '원형'이 있습니다. '히잉'의 원형은 `히잉`입니다. '꼬리'의 원형은 `꼬리`입니다. '살랑'의 원형은 `살랑`입니다. '호칭'은 상기 5가지 경우에 대해 각각의 원형이 존재합니다.

* 초성이 'ㄴ'인 경우 원형은 `누나야`입니다.
* 초성이 'ㅇ'이고 중성이 'ㅓ'인 경우 원형은 `언니야`입니다.
* 초성이 'ㅇ'이고 중성이 'ㅗ' 경우 원형은 `오빠야`입니다.
* 초성이 'ㅍ'인 경우 원형은 `필멸자야`입니다.
* 초성이 'ㅎ'인 경우 원형은 `형아`입니다.

구문의 각 단어는 원형과의 차이를 비교하여 삽입 글자수와 삭제 글자수의 두 수로 변환되며, 이를 변화량이라고 부르겠습니다.

삽입 글자수는 원형에 없는 한글 음절의 개수를 의미합니다. 예를 들어 구문의 첫번째 단어를 `히이잉`으로 작성한 경우, 삽입 글자수는 1입니다.

삭제 글자수는 원형에만 있는 한글 음절의 개수를 의미합니다. 예를 들어 구문의 세번째 단어를 `복슬`로 작성한 경우, 삭제 글자수는 2입니다.

앞으로 삽입 글자수 m과 삭제 글자수 n의 변화량은 +m-n으로 표기합니다.

### 실행 모형

살랑 프로그램이 실행되는 문맥은 다음 네 가지 요소로 구성됩니다.

* 실행 줄 번호
* 5개의 스택 (이름은 각각 '누나야', '언니야', '오빠야', '필멸자야', '형아'입니다.)
* 메모리
* 파일 스트림
* 놀이터

실행 줄 번호는 다음에 실행할 구문의 줄 번호입니다. 가장 첫번째 줄이 1번입니다. 특정 연산을 통해 이를 변경할 수 있습니다.

스택은 정수에 대해 '넣기' 연산과 '빼기' 연산을 할 수 있는 저장 공간입니다. '넣기' 연산을 하는 경우 스택 안에 값이 저장됩니다. '빼기' 연산을 하면 가장 나중에 '넣기'를 한 값이 반환되며, 반환된 값은 저장 공간에서 삭제됩니다.

메모리는 주소와 값을 연결 짓는 저장 공간입니다.
특정 주소에 값을 '쓰기'한 다음, 같은 주소에서 '읽기'를 하면 해당 값을 얻을 수 있습니다.
'읽기'를 하여도 저장된 값이 삭제되지는 않습니다.
이미 값이 쓰여진 주소에 다른 값을 쓰면 새로운 값으로 덮어씌워집니다.
'쓰기'를 하지 않은 주소에 '읽기'를 할 수 없습니다.

파일 스트림은 외부와 통신하는 통로입니다. '읽기'를 통해 1바이트를 수신하고, '쓰기'를 통해 1바이트를 전송할 수 있습니다. 파일 스트림에는 파일 기술자라는 값이 배정되어 있어서, 살랑 프로그램에서는 이를 통해 파일 스트림을 특정합니다. 특별히, 표준 입력은 0번, 표준 출력은 1번, 표준 에러 출력은 2번이 배정되어 있습니다.

놀이터는 해당 구문 내에서 임시로 값을 보관하는 곳입니다. 구문을 실행하며 그 내용이 변화할 수 있으며, 다음 구문으로 넘어가면 모두 삭제됩니다.

### 실행

각 구문은 '히잉', '호칭', '꼬리', '살랑' 순으로 실행한 다음, 다시 '히잉'과 '호칭'에 남은 작업이 있는 경우 이 순서로 실행합니다.

### 히잉
'히잉'은 해당 구문의 행동 중 실행 줄 번호와 관련된 것을 지정합니다.

'히잉'은 변화량에 따라 다음 행동을 수행합니다.
* +0-0 (`히잉`): 아무 행동도 하지 않습니다. 해당 구문이 모두 실행되고 나면 다음 줄을 실행합니다.
* +1-0 (`히이잉` 등): 놀이터에 다음 실행 줄 번호를 추가합니다.
* +0-1 (`히` 등): '살랑'에게 '가져올 값'이 하나 있음을 알려주고, '살랑'이 실행되고 나면 놀이터에 남은 첫번째 값을 가져와 다음 실행 줄 번호로 삼습니다. 가져온 값은 놀이터에서 삭제됩니다.
* +1-1 (`후잉` 등): `히이잉`과 `히`를 모두 수행합니다.

### 호칭
'호칭'은 해당 구문의 행동 중 스택과 관련된 것을 지정합니다.

'호칭'은 변화량에 따라 해당 호칭과 짝이 되는 스택에 대하여 다음 행동을 수행합니다.
* +0-0 (`형아` 등): 아무 행동도 하지 않습니다.
* +1-0 (`형아야` 등): '살랑'에게 '가져올 값'이 하나 있음을 알려주고, '살랑'에 이어 '히잉'까지 실행한 다음, 놀이터에 남은 첫번째 값을 가져와 짝이 되는 스택에 '넣기' 합니다. 가져온 값은 놀이터에서 삭제됩니다.
* +0-1 (`형` 등): 짝이 되는 스택에서 값을 하나 '빼기'하여 놀이터의 가장 나중에 추가합니다.
* +1-1 (`형도` 등): `형`과 `형아야`을 모두 수행합니다.
* +1-2 (`헥` 등): `형`을 두 번 수행 후 `형아야`를 수행합니다.

### 꼬리
'꼬리'는 삽입 글자수에서 삭제 글자수를 뺀 값을 놀이터의 가장 나중에 추가합니다.
다만, 삽입 글자수와 삭제 글자수가 모두 양수인 경우는 허용하지 않습니다.

### 살랑
'살랑'은 놀이터에 있는 값에 대해 연산을 수행합니다.

'살랑'은 변화량에 때라 다음 행동을 수행합니다.
* +0-0 (`살랑`):
    - '히잉'과 '호칭'을 실행하며 '가져올 값'의 개수가 전달됩니다.
    - 먼저, 놀이터의 마지막 값을 가져와 '주소'로 삼습니다. 가져온 값은 놀이터에서 삭제됩니다.
    - 이제 놀이터에 있는 값의 개수를 '가져올 값'의 개수와 비교합니다.
      만약 둘이 같다면 아무 일도 하지 않습니다.
    - '가져올 값'이 더 많으면, 메모리에서 '주소'에 해당하는 값을 가져옵니다. 단, '주소'를 파일 기술자로 갖는 파일 스트림이 있다면, 여기에서 1바이트를 대신 가져옵니다. 가져온 값은 놀이터의 가장 나중에 추가합니다.
    - 반대로 '가져올 값'이 더 적으면, 놀이터에서 값을 하나 가져와 메모리에 '주소'에 해당하는 곳에 저장합니다. 단, '주소'를 파일 기술자로 갖는 파일 스트림이 있다면 여기에 대신 전송합니다. 가져온 값은 놀이터에서 삭제됩니다.
* +1-0 (`살랑뀨` 등): 놀이터의 첫번째 값과 두번째 값을 가져와 더한 후 놀이터의 처음에 추가합니다. 가져온 값은 놀이터에서 삭제됩니다.
* +0-1 (`살` 등): 놀이터의 첫번째 값과 두번째 값을 가져와 첫번째 값에서 두번째 값을 뺀 후 놀이터의 처음에 추가합니다. 가져온 값은 놀이터에서 삭제됩니다.
* +2-0 (`살랑살랑` 등): 놀이터의 첫번째 값과 두번째 값을 가져와 곱한 후 놀이터의 처음에 추가합니다. 가져온 값은 놀이터에서 삭제됩니다.
* +0-2 (``): 놀이터의 첫번째 값과 두번째 값을 가져와 첫번째 값을 두번째 값으로 나눈 값의 정수부를 놀이터의 처음에 추가합니다. 가져온 값은 놀이터에서 삭제됩니다.
* +1-1 (`봐랑` 등): 놀이터의 첫번째 값을 가져와 양수인 경우 1, 음수인 경우 -1, 0인 경우 0을 놀이터의 처음에 추가합니다. 가져온 값은 삭제됩니다.

### 종료
실행 줄 번호가 프로그램 줄 수를 넘어가거나 0이하라면 프로그램을 종료합니다.
메모리에서 -1의 주소에 저장된 값이 있다면 그 값을 종료 코드로 하고, 없다면 0을 종료 코드로 합니다.

